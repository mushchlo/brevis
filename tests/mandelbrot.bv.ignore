let Bailout = 16.0;
let Maxiter = 1000;

let for_range = fn(low, high, f) {
	if low <= high {
		f(low);
		for_range(low + 1.0, high, f);
	}
};

let mandelbrot = fn(x, y) {
	let cr = y - 0.5;
	let ci = x;

	let aux = fn(i, zr, zi) {
		i = i + 1;
		let tmp = zr * zi;
		let zr2 = zr * zr;
		let zi2 = zi * zi;
		zr = zr2 - zi2 + cr;
		zi = tmp + tmp + ci;

		if zi2 + zr2 > Bailout {
			i
		} else if i > Maxiter {
			0
		} else {
			aux(i, zr, zi)
		}
	};

	aux(0, 0.0, 0.0)
};

let draw_mandelbrot = fn(size) {
	for_range(-size, size,
		fn(y) {
			for_range(-size, size,
				fn(x) {
					let i = mandelbrot(x/(size + 1.0), y/(size + 1.0));
					if i == 0 {
						print("*");
					} else {
						print(" ");
					}
				}
			);
			print("\n");
		}
	)
};

draw_mandelbrot(40.0);